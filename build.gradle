buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
    }
}
apply plugin: 'com.android.application'

/**
 * 在开始使用表情MM SDK之前，开发者需要首先申请到一对AppID和AppSecret，并用它们初始化SDK。
 * 为了运行本Demo，请在项目根目录下的local.properties中加入如下两行：
 * bqmm.appID=<申请到的AppID>
 * bqmm.appSecret=<申请到的AppSecret>
 *
 * 以下代码会从local.properties中读取这两个值
 */
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def bqmmAppID = properties.getProperty('bqmm.appID')
def bqmmAppSecret = properties.getProperty('bqmm.appSecret')

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
    defaultConfig {
        /**
         * 以下代码可以把appID和appSecret写入AndroidManifest
         */
        manifestPlaceholders = [bqmmAppID: bqmmAppID, bqmmAppSecret: bqmmAppSecret]
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
    compile project(':bQMM_Lib')
}